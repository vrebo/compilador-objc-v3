options{
    STATIC = false;
}

PARSER_BEGIN(Compilador)
package org.itver.objccompiler.model.compiler;

import java.io.File;
import java.io.FileNotFoundException;
import java.io.FileReader;
import java.util.ArrayList;
import java.util.List;
import org.itver.ob.*;
import org.itver.util.FileUtilities;



public class Compilador {

    private static final StringBuilder logAnalisisLexico;
    private static final StringBuilder logAnalisisSintactico;
    private static final StringBuilder logAnalisisSemantico;

    private static final List<LogMessage> lexicalLog;
    private static final List<LogMessage> syntaticLog;
    private static final List<LogMessage> semanticLog;

    private static int contadorErrores;

    static {
        logAnalisisLexico = new StringBuilder();
        logAnalisisSintactico = new StringBuilder();
        logAnalisisSemantico = new StringBuilder();
        lexicalLog = new ArrayList<LogMessage>();
        syntaticLog = new ArrayList<LogMessage>();
        semanticLog = new ArrayList<LogMessage>();
        contadorErrores = 0;
    }

    public static void registraLogLexico(String mensaje) {
        logAnalisisLexico.append(mensaje).append("\u005cn");
    }

    public static void registraLogSintactico(String mensaje) {
        logAnalisisSintactico.append(mensaje).append("\u005cn");
    }

    public static void registraLogSemantico(String mensaje) {
        contadorErrores++;
        logAnalisisSemantico.append(mensaje).append("\u005cn");
    }

    public static void registraErrorSintactico(String mensaje, Token token) {
        contadorErrores++;
        StringBuilder sb = new StringBuilder()
                .append(mensaje)
                .append("Linea: ").append(token.beginLine)
                .append(" Columna: ").append(token.beginColumn);
        logSyntacticErrorMessage(sb.toString());
        registraLogSintactico(sb.toString());
    }

        public static StringBuilder getLogAnalisisLexico() {
        return logAnalisisLexico;
    }

    public static StringBuilder getLogAnalisisSintactico() {
        return logAnalisisSintactico;
    }

    public static StringBuilder getLogAnalisisSemantico() {
        return logAnalisisSemantico;
    }

    public static void initLogs() {
        logAnalisisLexico.setLength(0);
        logAnalisisSintactico.setLength(0);
        logAnalisisSemantico.setLength(0);
    }

    public static void logLexicalErrorMessage(String message){
        lexicalLog.add(new LogErrorMessage(message));
    }

    public static void logSyntacticErrorMessage(String message){
        syntaticLog.add(new LogErrorMessage(message));
    }

    public static void logLexicalDebugMessage(String message){
        lexicalLog.add(new LogDebugMessage(message));
    }

    public static void logSystacticDebugMessage(String message){
        syntaticLog.add(new LogDebugMessage(message));
    }

    public static void logLexicalInfoMessage(String message){
        lexicalLog.add(new LogInfoMessage(message));
    }

    public static void logSystacticInfoMessage(String message){
        syntaticLog.add(new LogInfoMessage(message));
    }

    public static void logLexicalWarningMessage(String message){
        lexicalLog.add(new LogWarningMessage(message));
    }

    public static void logSystacticWarningMessage(String message){
        syntaticLog.add(new LogWarningMessage(message));
    }

    public static void logLexicalMessage(String message){
        lexicalLog.add(new LogMessage(message));
    }

    public static void logSystacticMessage(String message){
        syntaticLog.add(new LogMessage(message));
    }

    public static List<LogMessage> getLexicalLog(){
        return lexicalLog;
    }
    
    public static List<LogMessage> getSyntacticLog(){
        return syntaticLog;
    }
    
    private File codigo;

    public Compilador(File codigo) throws FileNotFoundException {
        this(new FileReader(codigo));
        this.codigo = codigo;
    }

    public void imprimeLogs() {
        imprimeAnalisisLexico();
        imprimeAnalisisSintactico();
        imprimeAnalisisSemantico();
    }

    private void imprimeAnalisisLexico() {
        imprimeArchivo("res/analsis_lexico_" + codigo.getName() + ".txt",
                Compilador.getLogAnalisisLexico().toString());
    }

    private void imprimeAnalisisSintactico() {
        imprimeArchivo("res/analsis_sintactico_" + codigo.getName() + ".txt",
                Compilador.getLogAnalisisSintactico().toString());
    }

    private void imprimeAnalisisSemantico() {
        imprimeArchivo("res/analsis_semantico_" + codigo.getName() + ".txt",
                Compilador.getLogAnalisisSemantico().toString());
    }

    private void imprimeArchivo(String nombreArchivo, String contenido) {
        try {
            FileUtilities.writeToFile(contenido, contenido);
        } catch (FileNotFoundException ex) {
            System.err.println("No se pudo encontrar el archivo");
        }
    }

    void skipTo(int kind) {
        Token tok = getToken(0);
        while (tok.kind != EOF && tok.kind != kind) {
            tok = getNextToken();
            System.out.println("Saltanto token: " + tok.image);
        }
    }

    void skipTo(String st) {
        Token tok = getToken(0);
        System.out.println("Token saltado " + tok.image);
        while (tok.kind != EOF && !tok.image.equals(st)) {
            tok = getNextToken();
        }
    }

    void skipTo(int[] left, int[] right) {
        Token prev = getToken(0);
        Token next = getToken(1);
        boolean flag = false;
        if (prev.kind == EOF || next.kind == EOF) {
            flag = true;
        }
        for (int i = 0; i < left.length; i++) {
            if (prev.kind == left[i]) {
                flag = true;
            }
        }
        for (int i = 0; i < right.length; i++) {
            if (next.kind == right[i]) {
                flag = true;
            }
        }

        while (!flag) {
            System.err.println("Token saltado: " + prev);
            getNextToken();
            prev = getToken(0);
            next = getToken(1);
            if (prev.kind == EOF || next.kind == EOF) {
                flag = true;
            }
            for (int i = 0; i < left.length; i++) {
                if (prev.kind == left[i]) {
                    flag = true;
                }
            }
            for (int i = 0; i < right.length; i++) {
                if (next.kind == right[i]) {
                    flag = true;
                }
            }
        }
    }

}
PARSER_END(Compilador)

//ÁREA DE TOKENS

//Tokens de operadores aritméticos
TOKEN:{
    <OP_INCREMENTO:"++">{
        Compilador.logLexicalInfoMessage("Operador de incremento encontrado".concat(image.toString()));
    }
    |<OP_DECREMENTO: "--">{
        Compilador.logLexicalInfoMessage("Operador de decremento encontrado".concat(image.toString()));
    }
    |<OP_SUMA : "+">{
        Compilador.logLexicalInfoMessage("Operador de adición encontrado".concat(image.toString()));
    }
    |<OP_RESTA : "-">{
        Compilador.logLexicalInfoMessage("Operador de sustracción encontrado".concat(image.toString()));
    }
    |<OP_PRODUCTO : "*">{
        Compilador.logLexicalInfoMessage("Operador de producto encontrado".concat(image.toString()));
    }
    |<OP_DIVISION : "/">{
        Compilador.logLexicalInfoMessage("Operador de cociente encontrado".concat(image.toString()));
    }
    |<COMPLEMENTO: "~" >{
        Compilador.logLexicalInfoMessage("Operador de complemento a 2 encontrado".concat(image.toString()));
    }
}

//Tokens de operadores lógicos y de bits
TOKEN : {
    <OP_AND : "&&">{
        Compilador.logLexicalInfoMessage("Operador AND lógico encontrado encontrado".concat(image.toString()));
    }
    |<OP_OR : "||">{
        Compilador.logLexicalInfoMessage("Operador OR lógico encontrado encontrado".concat(image.toString()));
    }
    |<OP_NOT : "!">{
        Compilador.logLexicalInfoMessage("Operador NOT lógico encontrado encontrado".concat(image.toString()));
    }
    |<OP_OR_BIT : "|">{
        Compilador.logLexicalInfoMessage("Operador OR a nivel de bits encontrado".concat(image.toString()));
    }
    |<OP_AND_BIT : "&">{
        Compilador.logLexicalInfoMessage("Operador AND a nivel de bits encontrado".concat(image.toString()));
    }
    |<OP_XOR_BIT : "^">{
        Compilador.logLexicalInfoMessage("Operador XOR a nivel de bits encontrado".concat(image.toString()));
    }
}

TOKEN : {
    <OP_ASIG : "=">{
        Compilador.logLexicalInfoMessage("Operador de asignación encontrado".concat(image.toString()));
    }
    |<OP_ASIG_SUMA : "+=">{
        Compilador.logLexicalInfoMessage("Operador de asignacion con adición encontrado".concat(image.toString()));
    }
    |<OP_ASIG_RESTA : "-=">{
        Compilador.logLexicalInfoMessage("Operador de asignacion con sustracción encontrado".concat(image.toString()));
    }
    |<OP_ASIG_PRODUCTO : "*=">{
        Compilador.logLexicalInfoMessage("Operador de asignacion con producto encontrado".concat(image.toString()));
    }
    |<OP_ASIG_COCIENTE : "/=">{
        Compilador.logLexicalInfoMessage("Operador de asignacion con cociente encontrado".concat(image.toString()));
    }
    |<OP_ASIG_MODULO : "%=">{
        Compilador.logLexicalInfoMessage("Operador de asignacion con modulo encontrado".concat(image.toString()));
    }
    |<OP_ASIG_SHIFT_LEFT : "<<=">{
        Compilador.logLexicalInfoMessage("Operador de asignacion con recorrimiento a la izquierda encontrado".concat(image.toString()));
    }
    |<OP_ASIG_SHIFT_RIGTH : ">>=">{
        Compilador.logLexicalInfoMessage("Operador de asignacion con recorrimiento a la derecha encontrado".concat(image.toString()));
    }
    |<OP_ASIG_XOR : "^=">{
        Compilador.logLexicalInfoMessage("Operador de asignacion con operación XOR a nivel de bits encontrado".concat(image.toString()));
    }
    |<OP_ASIG_OR : "|=">{
        Compilador.logLexicalInfoMessage("Operador de asignacion con operación OR a nivel de bits encontrado".concat(image.toString()));
    }
    |<OP_ASIG_AND : "&=">{
        Compilador.logLexicalInfoMessage("Operador de asignacion con operación AND a nivel de bits encontrado".concat(image.toString()));
    }
}

//Tokens para literales
TOKEN : {
    <LITERAL_BOOLEANA : ("YES"|"NO"|"true"|"false")>{
        Compilador.logLexicalInfoMessage("Literal booleana encontrada: ".concat(image.toString()));
    }
    |< LITERAL_ENTERA:
        <LITERAL_DECIMAL> (["l","L"])?
      | <LITERAL_HEX> (["l","L"])?
      | <LITERAL_OCTAL> (["l","L"])?>{
        Compilador.logLexicalInfoMessage("Literal entera encontrada: ".concat(image.toString()));
    }
    |
        < #LITERAL_DECIMAL: ["1"-"9"] (["0"-"9"])* >
    |
        < #LITERAL_HEX: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
    |
        < #LITERAL_OCTAL: "0" (["0"-"7"])* >
    |
        < LITERAL_FLOTANTE:
            (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENTE>)? (["f","F","d","D"])?
        | "." (["0"-"9"])+ (<EXPONENTE>)? (["f","F","d","D"])?
        | (["0"-"9"])+ <EXPONENTE> (["f","F","d","D"])?
        | (["0"-"9"])+ (<EXPONENTE>)? ["f","F","d","D"]
        >{
        Compilador.logLexicalInfoMessage("Literal flotante encontrada: ".concat(image.toString()));
    }
    |
        < #EXPONENTE: ["e","E"] (["+","-"])? (["0"-"9"])+ >
    |
    < LITERAL_CARACTER:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )
      "'"
      >{
        Compilador.logLexicalInfoMessage("Literal caracter encontrada: ".concat(image.toString()));
    }
    |
    < LITERAL_STRING:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
    >{
        Compilador.logLexicalInfoMessage("Literal string de C encontrada: ".concat(image.toString()));
    }
    |<LITERAL_STRING_OBJC:
        "@"<LITERAL_STRING>
    >{
        Compilador.logLexicalInfoMessage("Literal string de Objective-C encontrada: ".concat(image.toString()));
    }
}

//tokens para tipos de datos
TOKEN : {
    <BYTE : "byte">{
        Compilador.logLexicalInfoMessage("Palabra reservada de tipo de dato primitivo ".concat(image.toString()).concat(" encontrada"));
    }
    |<SHORT : "short">{
        Compilador.logLexicalInfoMessage("Palabra reservada de tipo de dato primitivo ".concat(image.toString()).concat(" encontrada"));
    }
    |<ENTERO : "int">{
        Compilador.logLexicalInfoMessage("Palabra reservada de tipo de dato primitivo ".concat(image.toString()).concat(" encontrada"));
    }
    |<LONG : "long">{
        Compilador.logLexicalInfoMessage("Palabra reservada de tipo de dato primitivo ".concat(image.toString()).concat(" encontrada"));
    }
    |<DOUBLE : "double">{
        Compilador.logLexicalInfoMessage("Palabra reservada de tipo de dato primitivo ".concat(image.toString()).concat(" encontrada"));
    }
    |<FLOTANTE: "float">{
        Compilador.logLexicalInfoMessage("Palabra reservada de tipo de dato primitivo ".concat(image.toString()).concat(" encontrada"));
    }
    |<BOOLEANO : "BOOL">{
        Compilador.logLexicalInfoMessage("Palabra reservada de tipo de dato primitivo ".concat(image.toString()).concat(" encontrada"));
    }
    |<CARACTER : "char">{
        Compilador.logLexicalInfoMessage("Palabra reservada de tipo de dato primitivo ".concat(image.toString()).concat(" encontrada"));
    }
    |<NSSTRING: "NSString">{
        Compilador.logLexicalInfoMessage("Palabra reservada de tipo de dato primitivo ".concat(image.toString()).concat(" encontrada"));
    }
    |<ARREGLO: "NSArray">{
        Compilador.logLexicalInfoMessage("Palabra reservada de tipo de dato primitivo ".concat(image.toString()).concat(" encontrada"));
    }
}

//tokens para palabras reservadas (sentencias de control)
TOKEN : {
    <VOID : "void">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<CONST : "const">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<FOR : "for">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<WHILE : "while">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<DO : "do">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<BREAK : "break">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<CONTINUE : "continue">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<RETURN : "return">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<IF : "if">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<ELSE : "else">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<SWITCH : "switch">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<CASE : "case">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
    |<PR_DEFAULT : "default">{
        Compilador.logLexicalInfoMessage("Palabra reservada ".concat(image.toString()).concat(" encontrada"));
    }
}

//tokens varios
TOKEN : {
    <IMPORTACION: "#import">{
        Compilador.logLexicalInfoMessage("Directiva de importación ".concat(image.toString()).concat(" encontrada"));
    }
    |<DELIMITADOR_INSTRUCCION : ";">{
        Compilador.logLexicalInfoMessage("Símbolo ".concat(image.toString()).concat(" encontrado"));
    }
    |<DOS_PUNTOS : ":">{
        Compilador.logLexicalInfoMessage("Símbolo ".concat(image.toString()).concat(" encontrado"));
    }
    |<COMA : ",">{
        Compilador.logLexicalInfoMessage("Símbolo ".concat(image.toString()).concat(" encontrado"));
    }
    |<PARENTESIS_APERTURA : "(">{
        Compilador.logLexicalInfoMessage("Símbolo ".concat(image.toString()).concat(" encontrado"));
    }
    |<PARENTESIS_CERRADURA : ")">{
        Compilador.logLexicalInfoMessage("Símbolo ".concat(image.toString()).concat(" encontrado"));
    }
    |<LLAVE_APERTURA : "{">{
        Compilador.logLexicalInfoMessage("Símbolo ".concat(image.toString()).concat(" encontrado"));
    }
    |<LLAVE_CERRADURA : "}">{
        Compilador.logLexicalInfoMessage("Símbolo ".concat(image.toString()).concat(" encontrado"));
    }
    |<CORCHETE_APERTURA : "[">{
        Compilador.logLexicalInfoMessage("Símbolo ".concat(image.toString()).concat(" encontrado"));
    }
    |<CORCHETE_CERRADURA : "]">{
        Compilador.logLexicalInfoMessage("Símbolo ".concat(image.toString()).concat(" encontrado"));
    }
    |<COMILLA_DOBLE : "\"">{
        Compilador.logLexicalInfoMessage("Símbolo ".concat(image.toString()).concat(" encontrado"));
    }
}

TOKEN : /* IDENTIFIERS */
{
  < ID: <LETRA> (<LETRA>|<DIGITO>)* >{
        Compilador.logLexicalInfoMessage("Identificador ".concat(" encontrado"));
    }
|
  < #LETRA:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGITO:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
>
    //|<PUNTERO: "*"<ID>>
}

//Skips
SKIP : {
    " "
    |"\r" 
    |"\t" 
    |"\n" 
}

//Los siguientes dos tokens sirven para omitir comentarios dentro del código. Fue
//encontrado en internet.
SKIP :{
    <"//"(~["\r", "\n"])*>
    | < "/*" > : DENTRO_COMENTARIO
}
<DENTRO_COMENTARIO> SKIP :{
    < "*/" > : DEFAULT
    | < ~[] >   
}

//ÁREA DE GRAMÁTICAS

void unidadCompilacion():{}{
    (importacion())*
    (
    LOOKAHEAD(3)declVariableGlobal()
    |declMetodo()    
    )*
    <EOF>
    {imprimeLogs();}
    {logSystacticMessage("Análisis sintáctico terminado con ".concat(contadorErrores + "").concat(" errores."));}
}

void importacion(): {
    int [] tokensIzquierda = {};
    int [] tokensDerecha = {
        IMPORTACION, VOID, BOOLEANO, BYTE, SHORT, ENTERO, LONG, FLOTANTE, 
        DOUBLE, CARACTER, NSSTRING, ARREGLO
    };
}{
    try{
        "#import" ( "\"" <ID> "\"" | "<" <ID> ">")
    {logSystacticInfoMessage("Drase de 'Directiva de importación' halláda.");}

    }catch(ParseException ex){
        registraErrorSintactico(
            "Error de sintaxis en directiva de importación ", ex.currentToken.next);
        skipTo(tokensIzquierda, tokensDerecha);
    }
}

/*  Gramática
*   DECL_VAR_GLOB   →       TIPO DECL_VAR
*                       |   TIPO DECL_VAR "," DECL_VAR_GLOB     */
void declVariableGlobal():{
    int [] tokensIzquierda = {
        DELIMITADOR_INSTRUCCION
    };
    int [] tokensDerecha = {
        DELIMITADOR_INSTRUCCION, VOID, BOOLEANO, BYTE, SHORT, ENTERO, LONG, FLOTANTE, 
        DOUBLE, CARACTER, NSSTRING, ARREGLO
    };
}{
try{
    tipo() ["[""]"] <ID> ["[""]"] [ "=" expresion() ]
}catch(ParseException ex){
    registraErrorSintactico(
            "Error de sintaxis en declaración de variable global ", ex.currentToken.next);
    skipTo(tokensIzquierda, tokensDerecha);
}
    (
try{
    "," <ID> [ "=" expresion() ]
}catch(ParseException ex){
    registraErrorSintactico(
            "Error de sintaxis en declaración de variable global ", ex.currentToken.next);
    skipTo(tokensIzquierda, tokensDerecha);
}
    )* 
try{
    ";"
    {logSystacticInfoMessage("Frasé de 'Declaración de variable global' halláda.");}
}catch(ParseException ex){
    registraErrorSintactico(
            "Error de sintaxis en declaración de variable global ", ex.currentToken.next);
}
{
    System.out.println("Gramática de declaración de variable global");
}
}

/*  Gramática
*   DECL_METODO →       TIPO_RESULTADO ID PARAMETROS BLOQUE
*                   |   TIPO_RESULTADO ID PARAMETROS ";"        */
void declMetodo():{
    int [] tokensIzquierda = {
        DELIMITADOR_INSTRUCCION
    };
    int [] tokensDerecha = {
        VOID, BOOLEANO, BYTE, SHORT, ENTERO, LONG, FLOTANTE, 
        DOUBLE, CARACTER, NSSTRING, ARREGLO
    };
}{
try{
    tipoResultado() <ID> parametros() ( bloque() | ";" )
    {logSystacticInfoMessage("Frase de 'Declaración de método' halláda.");}
}catch(ParseException ex){
    System.out.println(ex);
    registraErrorSintactico(
            "Error de sintaxis en declaración de método ", ex.currentToken.next);
    skipTo(tokensIzquierda, tokensDerecha);
}
}

/*  Gramática
*   PARAMETROS  →       "(" A ")"
*   A           →       EPSILON
*                   |   B
*   B           →       PARAMETRO
*                   |   PARAMETRO "," B     */
void parametros():{
    int [] tokensIzquierda = {
        DELIMITADOR_INSTRUCCION, PARENTESIS_CERRADURA, LLAVE_CERRADURA
    };
    int [] tokensDerecha = {
        LLAVE_APERTURA
    };
}{
try{
    "(" [ parametro() (","parametro() )* ] ")"
}catch(ParseException ex){
    registraErrorSintactico(
            "Error de sintaxis en declaración de método ", ex.currentToken.next);
    skipTo(tokensIzquierda, tokensDerecha);
}
    {logSystacticInfoMessage("Frase de 'Declaración de parámetros de método' halláda.");}
}

/*  Gramática
*   PARAMETRO   →       TIPO ID     */
void parametro():{
    int [] tokensIzquierda = {
        DELIMITADOR_INSTRUCCION
    };
    int [] tokensDerecha = {
        COMA, PARENTESIS_CERRADURA
    };
}{
try{
    ["const"] tipo() ["[""]"] <ID> ["[""]"]
}catch(ParseException ex){
    System.err.println("Error de sintaxis en declaración de método parametro " +  ex.currentToken.next.beginLine + " "  +ex.currentToken.next.beginColumn);
    registraErrorSintactico(
            "Error de sintaxis en declaración de método ", ex.currentToken.next);
    skipTo(tokensIzquierda, tokensDerecha);
}
}

/*  Gramática
*   TIPO    →   "byte" | "short" | "int" | "long" | "double"
*               | "float" | "BOOL" | "char" | "NSString" | "NSSArray"   */
void tipo():{}{
    <BYTE>|<SHORT>|<ENTERO>|<LONG>|<DOUBLE>|<FLOTANTE>
    |<BOOLEANO>|<CARACTER>|<NSSTRING>|<ARREGLO>
}

/*  Gramática
*   TIPO_RESULTADO  →   "void" | TIPO       */
void tipoResultado():{}{
    <VOID> | tipo()
}

/*  Gramática
*   EXPRESION   →   ASIGNACION | EXP_CONDICIONAL        */
void expresion():{}{
    LOOKAHEAD(expPrimaria() operadorAsignacion()) asignacion() | expCondicional()
    {logSystacticInfoMessage("Frase de 'Expresión' halláda.");}
}

/*  Gramática
*   ASIGNACION    →   EXP_PRIMARIA OP_ASIGNACION EXPRESION       */
void asignacion():{}{
    expPrimaria() operadorAsignacion() expresion()
}

/*  Gramática
*   OP_ASIGNACION  →    "=" | "*=" | "/=" | "%=" | "+=" | "-=" | "<<="
*                       ">>=" | "^=" | "|=" | "&="      */
void operadorAsignacion():{}{
    "="|"*="|"/="|"%="|"+="|"-="|"<<="|">>="|"^="|"|="|"&="
}

//Puede eliminarse
void expCondicional():{}{
    expOrCond()
}

void expOrCond():{}{
    expAndCond() ( "||" expAndCond() )*
}

void expAndCond():{}{
    expIor() ( "&&" expIor() )*
}

void expIor():{}{
    expXor() ( "|" expXor() )*
}

void expXor():{}{
    expAnd() ( "^" expAnd() )*
}

void expAnd():{}{
    expIgualdad() ( "&" expIgualdad() )*
}

void expIgualdad():{}{
    expRelacional() ( ("=="|"!=") expRelacional() )*
}

void expRelacional():{}{
    expRecorrimiento() ( ("<"|">"|"<="|">=") expRecorrimiento() )*
}

void expRecorrimiento():{}{
    expAdicion() ( ("<<"|">>") expAdicion() )*
}

void expAdicion():{}{
    expMultiplicacion() ( ("+"|"-") expMultiplicacion() )*
}

void expMultiplicacion():{}{
    expUnaria() ( ("*"|"/"|"%") expUnaria() )*
}

void expUnaria():{}{
    ("+"|"-")expUnaria() 
    | expPreInc() 
    | expPreDec() 
    | expUnariaNoMasMenos()
}

void expPreInc():{}{
    "++" expPrimaria()
}

void expPreDec():{}{
    "--" expPrimaria()
}

void expUnariaNoMasMenos():{}{
    ("~"|"!")expUnaria()
    | expPosfija()
}

void expPosfija():{}{
    expPrimaria() ["++"|"--"]
}

void expPrimaria():{
System.out.println("Gramática de expresión primaria");
}{
    literal() 
    | LOOKAHEAD(<ID> "(") llamadoFuncion() 
    | <ID> 
    | "(" expresion() ")"
}
/*FIXME
*
*/
void literal():{
    System.out.println("Gramática de literal");
}{
    <LITERAL_ENTERA>
    | <LITERAL_FLOTANTE>
    | <LITERAL_CARACTER>
    | <LITERAL_STRING>
    | <LITERAL_STRING_OBJC>
    | <LITERAL_BOOLEANA>
}

void llamadoFuncion():{
    System.out.println("Gramática de llamado de función");
}{
    <ID> argumentos()
    {logSystacticInfoMessage("Frase de 'Llamado a función' halláda.");}
}

void argumentos():{}{
    "(" [expresion() ( "," expresion() )* ] ")"
    {logSystacticInfoMessage("Frase de 'Argumentos de llamado de función' halláda.");}
}

void sentencia():{}{
    bloque()
    | sentVacia()
    | sentExp() ";"
    | sentSwitch()
    | sentIf()
    | sentWhile()
    | sentDo()
    | sentFor()
    | sentBreak()
    | sentContinue()
    | sentReturn()
}

void bloque():{
    int [] tokensIzquierda = {
    };
    int [] tokensDerecha = {
        DELIMITADOR_INSTRUCCION, LLAVE_APERTURA, LLAVE_CERRADURA,
        BOOLEANO, BYTE, SHORT, ENTERO, LONG, FLOTANTE, DOUBLE, CARACTER, 
        NSSTRING, ARREGLO, 
        FOR, WHILE, IF, DO, SWITCH, BREAK, CONTINUE, RETURN,
        ID, OP_INCREMENTO, OP_DECREMENTO
    };
}{
try{
    "{" 
}catch(ParseException ex){
    registraErrorSintactico(
            "Error de sintaxis en bloque de sentencias ", ex.currentToken.next);
    skipTo(tokensIzquierda, tokensDerecha);
}
    (bloqueSent())*
try{
    "}" 
    {logSystacticInfoMessage("Frase de 'Definición de bloque de código' halláda.");}
}catch(ParseException ex){
    registraErrorSintactico(
            "Error de sintaxis en bloque de sentencias ", ex.currentToken.next);
}
}

void bloqueSent():{}{
    declVarLoc() ";" | sentencia()
}

void declVarLoc():{}{
    tipo() <ID> [ "=" expresion() ] ("," <ID> [ "=" expresion() ] )*
}

void sentVacia():{}{
    ";"
}

void sentExp():{}{   
    expPreInc()
    | expPreDec() 
    | LOOKAHEAD(<ID> operadorAsignacion()) asignacion() 
    | LOOKAHEAD( <ID> "(" ) llamadoFuncion() 
    | expPosfija() 
}

void sentSwitch():{
    int [] tokensIzquierda = {
        LLAVE_APERTURA
    };
    int [] tokensDerecha = {
        DELIMITADOR_INSTRUCCION, LLAVE_CERRADURA,
        BOOLEANO, BYTE, SHORT, ENTERO, LONG, FLOTANTE, DOUBLE, CARACTER, 
        NSSTRING, ARREGLO, 
        FOR, WHILE, IF, DO, SWITCH, BREAK, CONTINUE, RETURN,
        ID, OP_INCREMENTO, OP_DECREMENTO
    };
}{
try{
    "switch" "(" expresion() ")" "{"
}catch(ParseException ex){
    registraErrorSintactico(
            "Error de sintaxis en sentencia switch ", ex.currentToken.next);
    skipTo(tokensIzquierda, tokensDerecha);
}
    ( LOOKAHEAD(":" | "case" | "default" | literal() ":") etiquetaSwitch() ( bloqueSent() )* )*
try{
    "}"
    {logSystacticInfoMessage("Frase de 'Sentencía switch-case' halláda.");}
}catch(ParseException ex){
    registraErrorSintactico(
            "Error de sintaxis en sentencia switch ", ex.currentToken.next);
}
}

void etiquetaSwitch():{
    int [] tokensIzquierda = {
        DOS_PUNTOS
    };
    int [] tokensDerecha = {
        DELIMITADOR_INSTRUCCION, LLAVE_CERRADURA,
        BOOLEANO, BYTE, SHORT, ENTERO, LONG, FLOTANTE, DOUBLE, CARACTER, 
        NSSTRING, ARREGLO, LITERAL_ENTERA,
        FOR, WHILE, IF, DO, SWITCH, BREAK, CONTINUE, RETURN, CASE, DEFAULT,
        ID, OP_INCREMENTO, OP_DECREMENTO
    };
}{   
try{
    "case"expresion() ":"  | "default" ":"
    {logSystacticInfoMessage("Frase de 'Etiqueta case' halláda.");}
}catch(ParseException ex){
    registraErrorSintactico(
            "Error de sintaxis en etiqueta de switch ", ex.currentToken.next);
    skipTo(tokensIzquierda, tokensDerecha);
}
}

void  sentIf():{
    String mensajeError = "";
}{
    (LOOKAHEAD("if" "(" expresion() ")") "if" "(" expresion() ")"
    | LOOKAHEAD("if" expresion() ")") "if"  expresion() ")" 
    {   mensajeError = "\t\t\t if EXPRESION ) \n";}
    | LOOKAHEAD(3)"if" "("  ")"
    {   mensajeError = "\t\t\t if ( ) \n";}
    | LOOKAHEAD("if" "(" (<ID> | literal())) "if" "(" expresion()
    {   mensajeError = "\t\t\t if ( EXPRESION \n";}
    | LOOKAHEAD(2) "if" "(" 
    {   mensajeError = "\t\t\t if ( \n";}
    | LOOKAHEAD(2) "if" ")"
    {   mensajeError = "\t\t\t if ) \n"; }
    | "if"
    {   mensajeError = "\t\t\t if  \n"; })
    sentencia()[ LOOKAHEAD(1) "else" sentencia() ]
{
    if(!mensajeError.isEmpty()){
        registraErrorSintactico(
            "Error de sintaxis en sentencia if\n" + mensajeError, getToken(0));
    }else{logSystacticInfoMessage("Frase de 'Sentencia if-else' halláda.");}
}
}

void sentWhile():{
    String mensajeError = "";
}{
    (LOOKAHEAD("while" "(" expresion() ")") "while" "(" expresion() ")" sentencia()
    | LOOKAHEAD("while" expresion() ")") "while"  expresion() ")" 
    {   mensajeError = "\t\t\t while EXPRESION ) \n";}
    | LOOKAHEAD(3)"while" "("  ")"
    {   mensajeError = "\t\t\t while ( ) \n";}
    | LOOKAHEAD("while" "(" (<ID> | literal())) "while" "(" expresion()
    {   mensajeError = "\t\t\t while ( EXPRESION \n";}
    | LOOKAHEAD(2) "while" "(" 
    {   mensajeError = "\t\t\t while ( \n";}
    | LOOKAHEAD(2) "while" ")"
    {   mensajeError = "\t\t\t while ) \n"; }
    |"while"
    {   mensajeError = "\t\t\t while  \n"; })
{
    if(!mensajeError.isEmpty()){
        registraErrorSintactico(
            "Error de sintaxis en sentencia while\n" + mensajeError, getToken(0));
    }else{logSystacticInfoMessage("Frase de 'Sentencia while' halláda.");}
}
}

void sentDo():{
    String mensajeError = "";
}{
    "do" sentencia() 
    (LOOKAHEAD("while" "(" expresion() ")") "while" "(" expresion() ")"
    | LOOKAHEAD("while" expresion() ")") "while"  expresion() ")" 
    {   mensajeError = "\t\t\t while EXPRESION ) \n";}
    | LOOKAHEAD(3)"while" "("  ")"
    {   mensajeError = "\t\t\t while ( ) \n";}
    | LOOKAHEAD("while" "(" (<ID> | literal())) "while" "(" expresion()
    {   mensajeError = "\t\t\t while ( EXPRESION \n";}
    | LOOKAHEAD(2) "while" "(" 
    {   mensajeError = "\t\t\t while ( \n";}
    | LOOKAHEAD(2) "while" ")"
    {   mensajeError = "\t\t\t while ) \n"; }
    | "while"
    {   mensajeError = "\t\t\t while  \n"; })
    ";"
{
    if(!mensajeError.isEmpty()){
        registraErrorSintactico(
            "Error de sintaxis en sentencia do - while\n" + mensajeError, getToken(0));
    }else{logSystacticInfoMessage("Frase de 'Sentencia do-while' halláda.");}
}
}

void sentFor():{
    String mensajeError = "";
}{
    (LOOKAHEAD("for" "(" cuerpoFor() ")")
        "for" "(" cuerpoFor() ")"
    | LOOKAHEAD("for" cuerpoFor() ")")
        "for" cuerpoFor() ")"
    {   mensajeError = "\t\t\t for ) \n";}
    | LOOKAHEAD("for" "(" cuerpoFor() )
            "for" "(" cuerpoFor()
    {   mensajeError = "\t\t\t for (  \n";}
    | LOOKAHEAD("for" "(" ")" )
        "for" "(" ")"
    {   mensajeError = "\t\t\t for ( ) \n";}
    | "for"
    {   mensajeError = "\t\t\t for ___ \n";})
    sentencia()
{
    if(!mensajeError.isEmpty()){
        registraErrorSintactico(
            "Error de sintaxis en sentencia for\n" + mensajeError, getToken(0));
    }else{logSystacticInfoMessage("Frase de 'Sentencia for' halláda.");}
}
}

void cuerpoFor():{
    String mensajeError = "";
}{
    (LOOKAHEAD([ forInit() ] ";" [ expresion() ] ";" [ forAct() ])
        [ forInit() ] ";" [ expresion() ] ";" [ forAct() ]
    | LOOKAHEAD([ forInit() ] ";" [ expresion() ]  [ forAct() ] )
        [ forInit() ] ";" [ expresion() ]  [ forAct() ] 
    {   mensajeError = "\t\t\t EXPRESION ; EXPRESION _ EXPRESION \n";}
    | LOOKAHEAD([ forInit() ] [ expresion() ] ";" [ forAct() ] )
            [ forInit() ] [ expresion() ] ";" [ forAct() ]
    {   mensajeError = "\t\t\t EXPRESION _ EXPRESION ; EXPRESION \n";}
    | LOOKAHEAD([ forInit() ] [ expresion() ] [ forAct() ] )
            [ forInit() ] [ expresion() ] [ forAct() ]
    {   mensajeError = "\t\t\t EXPRESION _ EXPRESION _ EXPRESION \n";}
    )
{
    if(!mensajeError.isEmpty()){
        registraErrorSintactico(
            "Error de sintaxis en sentencia for\n" + mensajeError, getToken(0));
    }
}
}

void forInit():{}{
    LOOKAHEAD(tipo() <ID>) declVarLoc() | listaSentExp()
}

void listaSentExp():{}{
    sentExp() ( "," sentExp() )*
}

void forAct():{}{
    listaSentExp()
}

void sentBreak():{}{
    "break" [<ID>] ";"
}

void sentContinue():{}{
    "continue" [<ID>] ";"
}

void sentReturn():{}{
    "return" [ expresion() ] ";"
}